plugins {
    id 'java'
    id 'maven-publish'
}

group 'io.github.thechampagne'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    // dependencies
}

task sourcesJar(type: Jar, dependsOn: classes) {
    archiveClassifier.set('sources')
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier.set('javadoc')
    from javadoc.destinationDir
}

publishing {
    repositories {
        maven {
            url = uri("https://maven.pkg.github.com/thechampagne/gradle-config")
            credentials {
                username = project.findProperty('GITHUB_USERNAME') // located in gradle.properties
                password = project.findProperty('GITHUB_TOKEN') // located in gradle.properties
            }
        }
    }
    publications {
        maven(MavenPublication) {
            artifact sourcesJar
            artifact javadocJar

            from components.java

            pom {
                name = 'gradle-config'
                description = 'Gradle config'
                url = 'https://github.com/thechampagne/gradle-config'
                licenses {
                    license {
                        name = 'MIT'
                        url = 'https://github.com/thechampagne/gradle-config/blob/main/LICENSE'
                    }
                }
                developers {
                    developer {
                        name = 'XXIV'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/thechampagne/gradle-config.git'
                    developerConnection = 'scm:git:ssh://github.com/thechampagne/gradle-config.git'
                    url = 'https://github.com/thechampagne/gradle-config'
                }
            }
        }
    }
}